#! /usr/bin/env escript
%%! -pa ./ebin -pa deps/getopt/ebin

-define(OPTS,
        [
         {help, undefined, "help", boolean,
          "Display usage information and exit"},
         {host, $h, "host", {string, "localhost"},
          "MQTT server hostname or IP address"},
         {port, $p, "port", {integer, 1883},
          "MQTT server port number"},
         {password, $P, "passwd", {string, "guest"},
          "Password for authentication"},
         {username, $u, "user", {string, "guest"},
          "Username for authentication"},
         {qos_level, $q, "qos", {integer, 0},
          "QoS level for the publication"},
         {retain, $r, "retain", {boolean, false},
          "Set the retain flag"}
        ]).

-define(ARGS,
        [{topic, "The topic to publish to"},
         {message, "The message to publish"}]).

main(Argv) ->
    {ok, {Opts, Args}} = getopt:parse(?OPTS, Argv),
    case proplists:get_value(help, Opts) of
        true ->
            ScriptPath = escript:script_name(),
            Script = filename:basename(ScriptPath),
            ArgString = [["<", atom_to_list(N), "> "] || {N, _} <- ?ARGS],
            HelpString = [{["<", atom_to_list(N), ">"], H} || {N, H} <- ?ARGS],
            getopt:usage(?OPTS, Script, ArgString, HelpString),
            halt(0);
        _ ->
            ok
    end,
    [Topic, Msg |_] = Args,
    [Host, Port, User, Passwd, QosInt, Retain] =
        [proplists:get_value(K, Opts) ||
            K <- [host, port, username, password, qos_level, retain]],
    QoS = erlmqtt:qos_symbol(QosInt),
    {ok, C} = erlmqtt:open_clean({Host, Port}, [{username, User},
                                                {password, Passwd}]),
    io:format("Publishing to ~s~n", [Topic]),
    erlmqtt:publish_sync(C, Topic, Msg, [QoS, {retain, Retain}]),
    erlmqtt:close(C).
