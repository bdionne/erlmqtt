#! /usr/bin/env escript
%% -*- erlang -*-
%%! -noinput -pa ./ebin -pa deps/getopt/ebin

-define(OPTS,
        [
         {help, undefined, "help", boolean,
          "Display usage information and exit"},

         {host, $h, "host", {string, "localhost"},
          "MQTT server hostname or IP address"},
         {port, $p, "port", {integer, 1883},
          "MQTT server port number"},
         {password, $P, "passwd", {string, "guest"},
          "Password for authentication"},
         {username, $u, "user", {string, "guest"},
          "Username for authentication"},
         {keep_alive, $k, "keepalive", {integer, 60},
          "Period for keep-alive ping, in seconds"},

         {qos_level, $q, "qos", {integer, 0},
          "QoS for subscriptions"},
         {topic, $t, "topic", string,
          "A topic to which to subscribe"}
        ]).

main(Argv) ->
    {ok, {Opts, _}} = getopt:parse(?OPTS, Argv),
    case proplists:get_value(help, Opts) of
        true ->
            ScriptPath = escript:script_name(),
            Script = filename:basename(ScriptPath),
            ArgString = "<topic> ...",
            HelpString = [{["<topic>"], "A topic to subscribe to"}],
            getopt:usage(?OPTS, Script, ArgString, HelpString),
            halt(0);
        _ ->
            ok
    end,
    [Host, Port] =
        [proplists:get_value(K, Opts) || K <- [host, port]],
    ConnectOpts = [O || O <- Opts, erlmqtt:is_connect_option(O)],
    {ok, C} = erlmqtt:open_clean({Host, Port}, ConnectOpts),

    QoS = erlmqtt:qos_symbol(proplists:get_value(qos_level, Opts)),
    Topics = proplists:get_all_values(topic, Opts),
    io:format("Subscribing to ~tp~n", [Topics]),
    erlmqtt:subscribe(C, [{T, QoS} || T <- Topics]),
    recv().

recv() ->
    {_T, Msg} = erlmqtt:recv_message(),
    io:format("~s~n", [Msg]),
    recv().
